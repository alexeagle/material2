package(default_visibility = ["//visibility:public"])

exports_files(["tsconfig.json"])

# TODO(jelbourn): figure out if these workarounds are still needed

# This rule belongs in node_modules/BUILD
# It's here as a workaround for
# https://github.com/bazelbuild/bazel/issues/374#issuecomment-296217940
filegroup(
  name = "node_modules",
  # Performance consideration: list individual files, reducing the number of
  # files as inputs to nodejs_binary:
  # bazel query "deps(:node_modules)" | wc -l
  # This won't scale in the general case, but works for now.
  # Note that this does *not* include Angular and RxJS because we build them
  # from source.
  srcs = glob(["/".join(["node_modules", pkg, "**", ext]) for pkg in [
    "@angular-devkit",
    "@schematics",
    "@types",
    "bytebuffer",
    "hammerjs",
    "jasmine",
    "minimist",
    "moment",
    "parse5",
    "protobufjs",
    "protractor",
    "reflect-metadata",
    "tsickle",
    "tslib",
    "tslint",
    "tsutils",
    "typescript",
    "zone.js",
  ] for ext in [
    "*.js",
    "*.json",
    "*.d.ts",
  ]] + [
    "node_modules/http-server/**",
  ]) + ["@build_bazel_rules_typescript//:node_modules"],
    # "@build_bazel_rules_typescript//:node_modules" is incluced
    # in `//:node_modules` so that npm dependencies that are hoisted to
    # node_modules/@bazel/typescript/node_modules because of conflicting
    # versions can be resolved correctly,
)


# Glob pattern that matches all Angular testing bundles.
ANGULAR_TESTING = [
  "node_modules/@angular/*/bundles/*-testing.umd.js",
  # The compiler and the dynamic platform-browser should be visible only in tests
  "node_modules/@angular/compiler/bundles/*.umd.js",
  "node_modules/@angular/platform-browser-dynamic/bundles/*.umd.js",
]

filegroup(
  name = "angular_bundles",
  srcs = glob(["node_modules/@angular/*/bundles/*.umd.js"], exclude = ANGULAR_TESTING),
)

filegroup(
  name = "angular_test_bundles",
  testonly = 1,
  srcs = glob(ANGULAR_TESTING),
)

filegroup(
  name = "tslib_bundle",
  testonly = 1,
  srcs = glob(["node_modules/tslib/tslib.js"]),
)

# Files necessary for unit tests that use zonejs
filegroup(
  name = "web_test_bootstrap_scripts",
  # The order of these deps is important.
  # Do not sort.
  srcs = [
    "//:node_modules/reflect-metadata/Reflect.js",
    "//:node_modules/zone.js/dist/zone.js",
    "//:node_modules/zone.js/dist/zone-testing.js",
    "//:node_modules/zone.js/dist/task-tracking.js",
  ],
)
